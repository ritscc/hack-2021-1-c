buildscript {
    ext {
        springBootVersion = "2.5.0"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath "org.flywaydb:flyway-gradle-plugin:7.9.1"
        classpath "org.yaml:snakeyaml:1.28"
        classpath "org.jacoco:org.jacoco.ant:0.8.7"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.flywaydb.flyway"
apply plugin: "java"
apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: "idea"

group = "dev.abelab"
version = "1.0"
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

configurations {
    mybatisGenerate
}

dependencies {
    // spring
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-websocket"
    implementation "org.springframework.boot:spring-boot-starter-validation"

    // spring security
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.security:spring-security-core"
    implementation "org.springframework.security:spring-security-web"
    implementation "org.springframework.security:spring-security-config"

    // JWT
    implementation "io.jsonwebtoken:jjwt:0.9.1"

    // JAXB API
    implementation "javax.xml.bind:jaxb-api:2.3.1"

    // flyway
    implementation "org.flywaydb:flyway-core"

    // lombok
    implementation "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    testImplementation "org.projectlombok:lombok:1.18.20"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"

    // mybatis
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0"
    implementation "org.mybatis.generator:mybatis-generator-maven-plugin:1.4.0"
    mybatisGenerate "org.mybatis.generator:mybatis-generator-core:1.4.0"
    mybatisGenerate "com.softwareloop:mybatis-generator-lombok-plugin:1.0"
    mybatisGenerate "mysql:mysql-connector-java"

    // mysql
    implementation "mysql:mysql-connector-java"

    // swagger
    implementation "io.springfox:springfox-boot-starter:3.0.0"
    implementation "io.springfox:springfox-swagger2:3.0.0"
    implementation "io.springfox:springfox-swagger-ui:3.0.0"

    // test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.jmockit:jmockit:1.49"

    // file type
    implementation "org.yaml:snakeyaml:1.28"
    implementation "net.rakugakibox.util:yaml-resource-bundle:1.2"
    implementation "com.google.code.gson:gson:2.8.7"

    // logging
    implementation "ch.qos.logback:logback-classic:1.2.3"

    // slack client
    implementation "com.slack.api:slack-api-client:1.8.1"
    implementation "com.squareup.okhttp3:okhttp:4.9.1"

    // util
    implementation "org.modelmapper:modelmapper:2.4.4"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "commons-net:commons-net:3.6"
}

bootJar{
    archiveBaseName = "timestamp"
}

bootRun {
    systemProperty "spring.profiles.active", "local"
    systemProperties System.properties
}

ext {
    // path config
    PROJECT_ROOT_PATH = projectDir.absolutePath.replaceAll("\\\\", "/")
    JAVA_ROOT_PATH = "${PROJECT_ROOT_PATH}/src/main/java"
    RESOURCES_ROOT_PATH = "app/src/main/resources"
    PROPERTY_ROOT_PATH = "${rootProject.rootDir.path}/${RESOURCES_ROOT_PATH}"
    GRADLE_ROOT_PATH = "../gradle"

    // run environment
    ENVIRONMENT =  System.getProperty("spring.profiles.active") ?: "local"

    // application property file
    BASE_PROPERTY_FILE = "application.yml"
    EXTENSION_PROPERTY_FILE = "application-${ENVIRONMENT}.yml"

    // application property
    BASE_PROPERTY = new org.yaml.snakeyaml.Yaml().load(new File("${PROPERTY_ROOT_PATH}/${BASE_PROPERTY_FILE}").newInputStream())
    EXTENSION_PROPERTY = new org.yaml.snakeyaml.Yaml().load(new File("${PROPERTY_ROOT_PATH}/${EXTENSION_PROPERTY_FILE}").newInputStream())

    // datasource
    DATASOURCE_URL = EXTENSION_PROPERTY.spring.datasource.url
    DATASOURCE_USERNAME = EXTENSION_PROPERTY.spring.datasource.username
    DATASOURCE_PASSWORD = EXTENSION_PROPERTY.spring.datasource.password
    DATASOURCE_DRIVER = EXTENSION_PROPERTY.spring.datasource.driver

    // mybatis
    MYBATIS_PROPERTY = BASE_PROPERTY.timestamp.mybatis
}

apply from: "${GRADLE_ROOT_PATH}/flyway.gradle"
apply from: "${GRADLE_ROOT_PATH}/javadoc.gradle"
apply from: "${GRADLE_ROOT_PATH}/mybatis.gradle"
apply from: "${GRADLE_ROOT_PATH}/test.gradle"
